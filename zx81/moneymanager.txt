10 PRINT "%(%C%)% %M%.%A%.%Y%O%U%N%G% %1%9%8%4"
15 PRINT AT 21,0;"PRESS ANY KEY TO START"
20 IF INKEY$="" THEN GOTO 20
30 CLS
40 PRINT TAB (9); "%I%N%S%T%R%U%C%T%I%O%N%S"
50 PRINT
60 PRINT "(1)IF YOU HAVE NOT ALREADY DONE SO, YOU %M%U%S%T BEGIN BY OPENING   AN ACCOUNT."
70 PRINT
80 PRINT "(2)YOU %C%A%N%N%O%T ASK TO ENTER DATA,REQUEST TRANSACTION DETAILS, NORSEE A STATEMENT UNLESS YOU  HAVEOPENED AN ACCOUNT PREVIOUSLY."
90 PRINT
100 PRINT "(3)WHERE NECESSARY KEYBOARD     INPUTS SHOULD BE FOLLOWED BY    PRESSING %N%E%W%L%I%N%E"
110 PRINT
120 PRINT "(4)THIS PROGRAM MAY BE USED IN  CONJUNCTION WITH A %Z%X %P%R%I%N%T%E%R   OR ANY OTHER COMPATIBLE PRINTER."
130 PRINT AT 21,0; "PRESS ANY KEY TO SEE THE MENU"
140 IF INKEY$="" THEN GOTO 140
150 CLS
160 FOR M=1 TO 8
170 PRINT AT 10,8; "*MONEY MANAGER*"
180 PRINT AT 9,9; "*************"
190 PRINT AT 11,9; "*************"
195 GOSUB 3520
220 PRINT AT 10,8; "*%M%O%N%E%Y% %M%A%N%A%G%E%R*"
230 NEXT M
240 CLS
250 PRINT "%M%O%N%E%Y% %M%A%N%A%G%E%R:-"
260 PRINT AT 6,0
270 PRINT "(1)OPEN A NEW ACCOUNT (THIS        DELETES ANY PREVIOUS ONE)"
280 PRINT "(2)ENTER DATA"
290 PRINT "(3)SEE STATEMENT"
300 PRINT "(4)SEE TRANSACTION DETAILS"
310 PRINT "(5)SAVE DATA"
320 PRINT "(6)SEE INSTRUCTIONS"
330 PRINT AT 21,0; "%T%Y%P%E% %R%E%Q%U%I%R%E%D% %N%U%M%B%E%R."
340 INPUT SEL
350 IF SEL=1 THEN GOSUB 420
360 IF SEL=2 THEN GOSUB 1510
370 IF SEL=3 THEN GOSUB 980
380 IF SEL=4 THEN GOSUB 2710
390 IF SEL=5 THEN GOTO 3080
400 IF SEL=6 THEN GOTO 30
410 GOTO 340
420 CLS
430 PRINT "WHAT NAME DO YOU WANT FOR THE   ACCOUNT?"
440 INPUT L$
450 PRINT
460 PRINT "ACCOUNT NAME IS \"";L$;"\""
470 PRINT AT 21,0;"PRESS ANY KEY TO CONTINUE"
480 IF INKEY$="" THEN GOTO 480
490 CLS
500 LET TOTAL=0
510 FOR I=1 TO 6
520 PRINT AT 11,9;"*NEW ACCOUNT*"
530 PRINT AT 10,10;"***********"
540 PRINT AT 12,10;"***********"
545 GOSUB 3520
570 PRINT AT 11,9;"*%N%E%W% %A%C%C%O%U%N%T*"
580 NEXT I
590 CLS
600 PRINT "NEW ACCOUNT"
610 LET N=1
620 DIM D$(N,5)
630 DIM E$(N,7)
640 DIM F(N)
650 DIM A(N)
660 FOR I=1 TO N
670 PRINT AT 20,0;"TYPE TODAYS DATE, E.G. 21APR (5)"
680 INPUT D$(I)
690 PRINT AT 20,0;"TYPE FORM OF PAYMENT, E.G. CASH OR CHEQUE (7)."
700 INPUT E$(I)
710 PRINT AT 20,0;"TYPE VALUE OF OPENING BALANCE"
720 INPUT F(I)
730 CLS
740 LET TOTAL=TOTAL+F(I)
750 LET A(I)=TOTAL
760 PRINT D$(I);TAB (7);E$(I);TAB (15);F(I);TAB (23);A(I)
770 NEXT I
780 PRINT AT 21,0;"PRESS ANY KEY TO CONTINUE"
785 IF INKEY$="" THEN GOTO 785
790 CLS
800 PRINT "ENTER ANY CHEQUE DETAILS"
810 LET P=1
820 DIM G$(P,5)
830 DIM F$(P,6)
840 DIM J$(P,8)
850 DIM K$(P,7)
860 FOR I=1 TO P
870 PRINT AT 20,0;"TYPE CHEQUE DATE, OR - (5)"
880 INPUT G$(I)
890 PRINT AT 20,0;"TYPE CHEQUE NUMBER, OR - (6)"
900 INPUT F$(I)
910 PRINT AT 20,0;"NAME OF PAYEE, OR - (8)"
920 INPUT J$(I)
930 PRINT AT 20,0;"TYPE CHEQUE VALUE, OR - (7)"
940 INPUT K$(I)
950 CLS
960 PRINT G$(I);TAB (6);F$(I);TAB (13);J$(I);TAB (25);K$(I)
970 NEXT I
975 GOTO 3460
980 CLS
990 FOR M=1 TO 6
1000 PRINT AT 11,10;"*STATEMENT*"
1010 PRINT AT 12,11;"*********"
1020 PRINT AT 10,11;"*********"
1025 GOSUB 3520
1050 PRINT AT 11,10;"*%S%T%A%T%E%M%E%N%T*"
1060 NEXT M
1070 CLS
1080 IF P<=18 THEN GOTO 3480
1087 FOR I=1 TO P STEP 10
1090 PRINT "(";I;")";D$(I),
1100 NEXT I
1110 PRINT AT 20,0;"TYPE THE NUMBER OF THE DATE YOU WANT THE STATEMENT TO START"
1120 INPUT K
1130 LET D=0
1140 CLS
1150 PRINT "DATE";TAB (7);"DETAILS";TAB (15);"CR/DR";CHR$(12);TAB (23);"BALANCE";CHR$(12)
1160 PRINT "\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::\::"
1170 FOR I=K TO P
1180 LET D=D+1
1190 IF D>=19 THEN GOTO 1300
1200 PRINT "\: ";D$(I);TAB (6);"\: ";TAB (7);E$(I);TAB (14);"\: ";TAB (15);F(I);TAB (22);"\: ";TAB (23);A(I);TAB (30);"\: "
1210 NEXT I
1220 PRINT "END OF STATEMENT"
1230 PRINT AT 21,0;"PRESS M:MENU P:PRINTOUT"
1240 IF INKEY$="" THEN GOTO 1240
1250 IF INKEY$="M" THEN GOTO 240
1260 IF INKEY$="P" THEN GOTO 1440
1270 GOTO 1230
1300 SCROLL
1310 PRINT AT 19,0;"|";D$(I);TAB(6);"|";TAB(7);E$(I);TAB(14);"|";TAB(15);F(I);TAB(22);"|";TAB(23);A(I);TAB(30);"|"
1320 PRINT AT 21,0;"[:CONT. P:PRINTOUT M:MENU"
1330 IF INKEY$="" THEN GOTO 1330
1340 IF INKEY$="P" THEN GOTO 1440
1350 IF INKEY$="M" THEN GOTO 240
1360 PRINT AT 21,0;""
1370 NEXT I
1380 PRINT AT 20,0;"END OF STATEMENT"
1390 GOTO 1230
1400 PRINT AT 21,0;"PRESS ANY KEY TO RETURN TO MENU"
1410 PRINT AT 21,0;"ACCOUNT NAME ";L$
1420 COPY
1430 GOTO 1270
1440 UNPLOT 0,0
1450 LPRINT "DATE";TAB(7);"DETAILS";TAB(15);"CR/DR£";TAB(23);"BALANCE"
1460 LPRINT
1470 PRINT AT 21,0;""
1480 PRINT AT 21,0;"ACCOUNT NAME ";L$
1490 COPY
1500 GOTO 1230
1510 CLS
1520 FOR I=1 TO 6
1530 PRINT AT 11,9;"*ENTER DATA*"
1540 PRINT AT 12,10;"************"
1550 PRINT AT 10,10;"************"
1555 GOSUB 3520
1580 PRINT AT 11,9;"*ENTER DATA*"
1590 NEXT I
1600 CLS
1610 PRINT "ENTER DATA"
1620 PRINT AT 10,0
1630 PRINT " (1) ENTER CREDITS AND DEBITS"
1640 PRINT " (2) ENTER CHEQUE DETAILS"
1650 PRINT AT 21,0;"TYPE REQUIRED NUMBER"
1660 INPUT ED
1670 IF ED=1 THEN GOSUB 1700
1680 IF ED=2 THEN GOSUB 2200
1690 RETURN
1700 CLS
1710 GOSUB 3390
1720 PRINT
1730 PRINT "ONE TRANSACTION USES 0.024K"
1740 PRINT
1750 PRINT "HOW MANY TRANSACTIONS?"
1760 INPUT M
1765 PRINT
1770 PRINT M
1780 DIM P$(P,5)
1790 DIM Q$(P,7)
1800 DIM R(P)
1810 DIM Z(P)
1815 PRINT
1820 PRINT "WAIT WHILE I MAKE SPACE"
1830 FOR I=1 TO P
1840 LET P$(I)=D$(I)
1850 LET Q$(I)=E$(I)
1860 LET R(I)=F(I)
1870 LET Z(I)=A(I)
1880 NEXT I
1890 DIM D$(P+M,5)
1900 DIM E$(P+M,7)
1910 DIM F(P+M)
1920 DIM A(P+M)
1930 FOR I=1 TO P
1940 LET D$(I)=P$(I)
1950 LET E$(I)=Q$(I)
1960 LET F(I)=R(I)
1970 LET A(I)=Z(I)
1980 NEXT I
1990 CLS
2000 PRINT "DONE-READY FOR EXTRA DATA"
2010 PRINT
2020 DIM P$(1)
2030 DIM Q$(1)
2040 DIM R(1)
2045 LET J=0
2050 FOR I=P+1 TO P+M
2055 LET J=J+1
2060 PRINT AT 18,0;"TRANSACTION ";J;" OF ";M
2065 PRINT AT 19,0;"THIS ENTRY NO. ";I
2070 PRINT AT 21,0;"ENTER DATE (5)"
2075 INPUT D$(I)
2080 PRINT AT 21,0;"ENTER DETAILS (7)"
2090 INPUT E$(I)
2100 PRINT AT 21,0;"ENTER VALUE"
2110 INPUT F(I)
2120 LET TOTAL=TOTAL+F(I)
2130 LET A(I)=TOTAL
2140 CLS
2150 PRINT D$(I);TAB(7);E$(I);TAB(15);F(I);TAB(23);A(I)
2160 NEXT I
2170 LET P=P+M
2180 GOSUB 3420
2190 RETURN
2200 CLS
2210 GOSUB 3390
2220 PRINT
2230 PRINT "ONE CHEQUE USES 0.024K"
2240 PRINT
2250 PRINT "HOW MANY CHEQUES?"
2260 INPUT M
2265 PRINT
2270 PRINT M
2280 DIM R$(N,5)
2290 DIM S$(N,6)
2300 DIM T$(N,8)
2310 DIM W$(N,7)
2315 PRINT
2320 PRINT "WAIT WHILE I MAKE SPACE"
2330 FOR I=1 TO N
2340 LET R$(I)=G$(I)
2350 LET S$(I)=F$(I)
2360 LET T$(I)=J$(I)
2370 LET W$(I)=K$(I)
2390 NEXT I
2400 DIM G$(N+M,5)
2410 DIM F$(N+M,6)
2420 DIM J$(N+M,8)
2430 DIM K$(N+M,7)
2440 FOR I=1 TO N
2450 LET G$(I)=R$(I)
2460 LET F$(I)=S$(I)
2470 LET J$(I)=T$(I)
2480 LET K$(I)=W$(I)
2490 NEXT I
2495 CLS
2500 PRINT "DONE-READY FOR EXTRA DATA"
2510 PRINT
2520 DIM R$(1)
2530 DIM S$(1)
2540 DIM T$(1)
2550 DIM W$(1)
2555 LET G=0
2560 FOR I=N+1 TO N+M
2561 LET G=G+1
2565 PRINT AT 18,0;"CHEQUE NO. ";G;" OF ";M
2570 PRINT AT 19,0;"THIS ENTRY NO. ";I
2575 PRINT AT 21,0;"ENTER CHEQUE DATE (5)"
2580 INPUT G$(I)
2590 PRINT AT 21,0;"ENTER CHEQUE NUMBER (6)"
2600 INPUT F$(I)
2610 PRINT AT 21,0;"ENTER NAME (8)"
2620 INPUT J$(I)
2630 PRINT AT 21,0;"ENTER CHEQUE VALUE (7)"
2640 INPUT K$(I)
2650 CLS
2660 PRINT G$(I);TAB (7);F$(I);TAB (15);J$(I);TAB (23);K$(I)
2670 NEXT I
2680 LET N=N+M
2690 GOSUB 3420
2700 RETURN
2710 CLS
2720 FOR I=1 TO 6
2730 PRINT AT 11,5;"*TRANSACTION DETAILS*"
2740 PRINT AT 12,6;"*********************"
2750 PRINT AT 10,6;"*********************"
2755 GOSUB 3520
2780 PRINT AT 11,5;"*TRANSACTION DETAILS*"
2790 NEXT I
2800 CLS
2815 IF N<=18 THEN GOTO 3500
2817 FOR I=1 TO N STEP 10
2820 PRINT "(";I;")";G$(I),
2830 NEXT I
2840 PRINT
2850 PRINT AT 20,0;"TYPE THE NUMBER FOR THE DATE THE LIST TO START"
2860 INPUT V
2870 LET W=0
2880 CLS
2890 PRINT "DATE.";TAB(7);"CHQ.NO.";TAB(14);"PAYEE.";TAB(23);"VALUE(£)"
2900 PRINT
2910 FOR I=V TO N
2920 LET W=W+1
2930 IF W>=19 THEN GOTO 3220
2940 PRINT "|";TAB (1);G$(I);TAB (6);"|";TAB (7);F$(I);TAB (13);"|";TAB (14);J$(I);TAB (22);"|";TAB (23);K$(I);TAB (31);"|"
2950 NEXT I
2960 PRINT "END OF TRANSACTIONS"
2970 PRINT AT 21,0;"PRESS M FOR MENU"
2980 IF INKEY$="" THEN GOTO 2980
2990 IF INKEY$="P" THEN GOTO 3040
3000 IF INKEY$="M" THEN GOTO 240
3010 PRINT AT 21,0;"PRESS ANY KEY TO RETURN TO MENU"
3020 IF INKEY$="" THEN GOTO 3020
3030 GOTO 240
3040 PRINT AT 21,0;""
3050 PRINT AT 21,0;"ACCOUNT NAME ";L$
3060 COPY
3070 GOTO 3010
3080 CLS
3090 PRINT "NAME OF PROGRAM?"
3100 INPUT V$
3110 PRINT
3120 PRINT "PROGRAM NAME IS ";V$
3130 PRINT
3140 PRINT "PRESS PLAY AND RECORD"
3150 PRINT
3160 PRINT "PRESS ANY KEY"
3170 IF INKEY$="" THEN GOTO 3170
3180 CLS
3190 SAVE V$
3200 GOTO 10
3220 SCROLL
3230 PRINT AT 19,0;"|";TAB (1);G$ (I);TAB (6);"|";TAB (7);F$(I); TAB (13);"|";TAB (14);J$(I);TAB (22);"|";TAB (23);K$(I);TAB (31);"|"
3240 PRINT AT 21,0;"[:TO CONT. P:PRINT M:MENU"
3250 IF INKEY$="" THEN GOTO 3250
3260 IF INKEY$="P" THEN GOTO 3320
3270 IF INKEY$="M" THEN GOTO 240
3280 PRINT AT 21,0;""
3290 NEXT I
3300 PRINT AT 20,0;"END OF TRANSACTIONS"
3310 GOTO 2970
3320 UNPLOT 0,0
3330 LPRINT "DATE.";TAB(7);"CHQ.NO.";TAB(14);"PAYEE.";TAB(23);"VALUE(£)"
3340 LPRINT
3350 PRINT AT 21,0;""
3360 PRINT AT 21,0;"ACCOUNT NAME ";L$
3370 COPY
3380 GOTO 3010
3390 LET H=PEEK 16386+256*PEEK 16387-PEEK 16412-256*PEEK 16413
3400 PRINT "SPARE MEMORY = ";H/1000;"K"
3410 RETURN
3420 PRINT AT 19,0;"PRESS M TO RETURN TO MAIN MENU OR E TO RETURN TO EDIT DATA"
3430 IF INKEY$="" THEN GOTO 3430
3440 IF INKEY$="E" THEN GOTO 1600
3450 GOTO 240
3460 PRINT AT 21,0;"PRESS ANY KEY TO CONTINUE"
3465 IF INKEY$="" THEN GOTO 3465
3470 GOTO 240
3480 LET K=1
3485 GOTO 1130
3490 REM (Unused)
3500 LET V=1
3510 GOTO 2870
3520 RETURN